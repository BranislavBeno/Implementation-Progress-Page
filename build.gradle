plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'jacoco'
    id 'org.sonarqube' version '5.0.0.4638'
    id 'org.cyclonedx.bom' version '1.8.2'
    id 'org.openrewrite.rewrite' version '6.16.0'
}

group = 'com.progress.application'

def versionMajor = 1
def versionMinor = 4
def versionPatch = 0
version = "R${versionMajor}.${versionMinor}.${versionPatch}" as Object

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext {
    set('sonarProjectName', 'ImplementationProgressPage') as Object
    set('sonarProjectKey', 'com.progress.application.project') as Object
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "com.j2html:j2html:1.6.0"
    implementation "commons-io:commons-io:2.16.1"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mockserver"
    testImplementation "org.mock-server:mockserver-client-java:5.15.0"
    testImplementation "org.apache.commons:commons-text:1.12.0"
    testImplementation "org.mockito:mockito-junit-jupiter:5.12.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.10.2"

    rewrite "org.openrewrite.recipe:rewrite-migrate-java:2.17.0"
    rewrite "org.openrewrite.recipe:rewrite-spring:5.12.0"
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:1.19.8"
    }
}

rewrite {
    activeRecipe("com.progress.application.project.NoStaticImport")
    activeRecipe("org.openrewrite.java.RemoveUnusedImports")
    activeRecipe("org.openrewrite.java.OrderImports")
    activeRecipe("org.openrewrite.java.migrate.UpgradeToJava21")
    activeRecipe("org.openrewrite.java.spring.boot3.SpringBoot3BestPractices")
    activeRecipe("org.openrewrite.java.spring.boot3.UpgradeSpringBoot_3_3")
}

bootJar {
    archiveFileName = "impl-progress.jar"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

jacoco {
    toolVersion = '0.8.12'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "BranislavBeno_ImplementationProgressPage"
        property "sonar.projectName", "implementation-progress-page"
    }
}
