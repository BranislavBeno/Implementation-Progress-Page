plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.1'
    id 'jacoco'
    id 'org.sonarqube' version '4.2.1.3168'
}

group = 'com.progress.application'

def versionMajor = 1
def versionMinor = 4
def versionPatch = 0
version = "R${versionMajor}.${versionMinor}.${versionPatch}" as Object

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}

ext {
    set('sonarProjectName', 'ImplementationProgressPage') as Object
    set('sonarProjectKey', 'com.progress.application.project') as Object
}

repositories {
    mavenCentral()
}

var bouncyCastleVersion = "1.70"
dependencies {
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "com.j2html:j2html:1.6.0"
    implementation "commons-io:commons-io:2.13.0"
    implementation "org.yaml:snakeyaml:2.0"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mockserver"
    testImplementation "org.mock-server:mockserver-client-java:5.15.0"
    testImplementation "org.bouncycastle:bcprov-jdk15on:${bouncyCastleVersion}"
    testImplementation "org.bouncycastle:bcpkix-jdk15on:${bouncyCastleVersion}"
    testImplementation "org.apache.commons:commons-text:1.10.0"
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:1.18.3"
    }
}

bootJar {
    archiveFileName = "impl-progress.jar"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

jacoco {
    toolVersion = '0.8.10'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "BranislavBeno_ImplementationProgressPage"
        property "sonar.projectName", "implementation-progress-page"
    }
}
