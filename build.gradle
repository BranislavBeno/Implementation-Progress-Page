plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.sonarqube' version '7.0.1.6134'
    id 'org.cyclonedx.bom' version '3.0.1'
    id 'org.openrewrite.rewrite' version '7.20.0'
}

group = 'com.progress.application'

def versionMajor = 1
def versionMinor = 4
def versionPatch = 0
version = "R${versionMajor}.${versionMinor}.${versionPatch}" as Object

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

ext {
    set('sonarProjectName', 'ImplementationProgressPage') as Object
    set('sonarProjectKey', 'com.progress.application.project') as Object
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "com.j2html:j2html:1.6.0"
    implementation "commons-io:commons-io:2.20.0"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation(platform("org.testcontainers:testcontainers-bom:2.0.1"))
    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mockserver"
    testImplementation "org.mock-server:mockserver-client-java:5.15.0"
    testImplementation "org.apache.commons:commons-text:1.14.0"

    rewrite(platform("org.openrewrite.recipe:rewrite-recipe-bom:3.18.0"))
    rewrite "org.openrewrite.recipe:rewrite-migrate-java"
    rewrite "org.openrewrite.recipe:rewrite-spring"
}

rewrite {
    activeRecipe("com.progress.application.project.NoStaticImport")
    activeRecipe("org.openrewrite.java.RemoveUnusedImports")
    activeRecipe("org.openrewrite.java.OrderImports")
    activeRecipe("org.openrewrite.java.migrate.UpgradeToJava25")
    activeRecipe("org.openrewrite.java.spring.boot3.SpringBoot33BestPractices")
    activeRecipe("org.openrewrite.java.spring.boot3.UpgradeSpringBoot_3_5")
}

bootJar {
    archiveFileName = "impl-progress.jar"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

jacoco {
    toolVersion = '0.8.14'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "BranislavBeno_ImplementationProgressPage"
        property "sonar.projectName", "implementation-progress-page"
    }
}
