plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'jacoco'
    id 'org.sonarqube' version '3.4.0.2513'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

group = 'com.progress.application'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(18)
    }
}

ext {
    set('sonarProjectName', 'ImplementationProgressPage') as Object
    set('sonarProjectKey', 'com.progress.application.project') as Object
}

repositories {
    mavenCentral()
}

var bouncyCastleVersion = "1.70"
dependencies {
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "com.j2html:j2html:1.6.0"
    implementation "commons-io:commons-io:2.11.0"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mockserver"
    testImplementation "org.mock-server:mockserver-client-java:5.14.0"
    testImplementation "org.bouncycastle:bcprov-jdk15on:${bouncyCastleVersion}"
    testImplementation "org.bouncycastle:bcpkix-jdk15on:${bouncyCastleVersion}"
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:1.17.3"
    }
}

bootJar {
    archiveFileName = "impl-progress.jar"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "BranislavBeno_ImplementationProgressPage"
    }
}